// fltk 7Fl_Tile 34 86 1031 349
[
// fltk N4xcas7EditeurE 34 86 1031 348
1323 ,
chaine2n(m):={
  //chaine2n(m) transforme la chaine m en l'entier n 
  //m est l'ecriture de n dans la base 256 
  local l,n,s;
  s:=size(m);
  l:=asc(m);
  n:=0;
  for (k:=0;k<s;k++){
    n:=n*256+l[k];
  }
  return(n);
};

ecritu256(n):={
  //transforme l'entier n en son ecriture en base 256
  local s,r;
  //n est un entier et b=256, ecritu256 est une fonction iterative 
  //ecritu256(n)=le mot de caracteres l'ecriture de n en base 256 
  s:="";
  while (n>=256){
    r:=irem(n,256);
    r:=char(r);
    s:=r+s;
    n:=iquo(n,256);
  }
  n:=char(n);
  s:=n+s;
  return(s);
};

//mess est une chaine u:=115769854373006801 n:=152415790094497781
codrsa(mess,u,n):={
  local s,j,j3,l,mot;
  s:=size(mess);
  j:=0;
  j3:=3;
  l:=[];
  //j est le nombre de paquets de 3 lettres
  while (j3<s) {
    mot:="";
    for (k:=j;k<j3;k++){
      mot:=mot+mess[k];
    }
    //on code le mot
    a:=chaine2n(mot);
    l:=append(l,powmod(a,u,n));
    j:=j3;
    j3:=j+3;
  }
  mot:="";
  for (k:=j;k<s;k++){
    mot:=mot+mess[k];
  }
  a:=chaine2n(mot);
  l:=append(l,powmod(a,u,n));
  return(l);
};

//l=codrsa(mess,u,n) m:=12345701 n:=152415790094497781
decodrsa(l,m,n):={
  local mess,s,a,j,b;
  s:=size(l);
  mess:="";
  for (j:=0;j<s;j++){
    b:=l[j];
    a:=powmod(b,m,n);
    mess:=mess+ecritu256(a);
  }
  return(mess);
};
,
// fltk N4xcas10Log_OutputE 34 434 1031 1

]
,
// fltk 7Fl_Tile 34 437 1031 74
[
// fltk N4xcas19Multiline_Input_tabE 34 437 1031 30
mes:=codrsa("bonjour",115769854373006801,152415790094497781)
,
// fltk N4xcas10Log_OutputE 34 467 1031 1

,
// fltk N4xcas8EquationE 34 468 1031 43
[134013005366630173,66011509696627353,146933964211893603]
]
,
// fltk 7Fl_Tile 34 513 1031 74
[
// fltk N4xcas19Multiline_Input_tabE 34 513 1031 30
decodrsa(mes,12345701,152415790094497781)
,
// fltk N4xcas10Log_OutputE 34 543 1031 1

,
// fltk N4xcas8EquationE 34 544 1031 43
"bonjour"
]
,
// fltk 7Fl_Tile 34 589 1031 31
[
// fltk N4xcas19Multiline_Input_tabE 34 589 1031 30

,
// fltk N4xcas10Log_OutputE 34 619 1031 1

]
