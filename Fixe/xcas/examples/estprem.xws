// fltk 7Fl_Tile 34 -290 1031 349
[
// fltk N4xcas7EditeurE 34 -290 1031 348
1085 ,
//renvoie la liste des nombres premiers<=n selon erathostene
crible(n):={
  local tab,prem,p;
  tab:=[0,0];
  prem:=[];
  for (j:=2;j<=n;j++){
    tab:=append(tab,j);
  }
  p:=2;
  while (p*p<=n) {
    for (j:=p;j*p<=n;j++){
      tab[eval(j*p)]:=0;
    }
    p:=p+1;
    while ((p*p<=n) and (tab[p]==0)) {
      p:=p+1;
    } 
  }
  for (j:=2;j<=n;j++) {
    if (tab[j]!=0) { 
      prem:=append(prem,j);
    }
  } 
  return(prem);
};

estprem(n):={
  //teste si n est premier
  local prem,j,k;
  if ((irem(n,2)==0) or (irem(n,3)===0) or (n==1)) {
    return(false);
  }
  if ((n==2) or (n==3)) {
    return(true);
  }
  prem:=true;
  k:=5;
  while ((k*k<=n) and prem) {
    if (irem(n,k)==0 or irem(n,k+2)==0) {
      prem:=false;
    }
    else {
      k:=k+6;
    }
  }
  return(prem);
};

estpremc(n):={
  //utilise la fonction crible (fichier erathostene) pour tester si n est premier
  local prem,s;
  prem:=crible(floor(sqrt(n)));
  s:=size(prem);
  if (n==1) return(false);
  for (k:=0;k<s;k++){
    if (irem(n,prem[k])==0){
      return(false);
    } 
  }
  return(true);
};
,
// fltk N4xcas10Log_OutputE 34 58 1031 1

]
,
// fltk 7Fl_Tile 34 61 1031 74
[
// fltk N4xcas19Multiline_Input_tabE 34 61 1031 30
estprem(29)
,
// fltk N4xcas10Log_OutputE 34 91 1031 1

,
// fltk N4xcas8EquationE 34 92 1031 43
true
]
,
// fltk 7Fl_Tile 34 137 1031 349
[
// fltk N4xcas7EditeurE 34 137 1031 348
1012 ,
rabin(n):={
  // teste par la methode de rabin si n est pseudo-premier;
  local k,j,p;
  j:=1;
  p:=1;
  while ((p==1) and (j<20)) {
    k:=2+rand(n-2);
    p:=powmod(k,n-1,n);
    j:=j+1;
  }
  if (p==1) {
    return(true);
  } 
  return(false);
};

miller(n):={
  local p,q,t,c,a,b,ex;
  if (n==2){
    return(true);
  }
  if (irem(n,2)==0) {
    return(false);
  }
  q:=n-1;
  t:=0;
  while (irem(q,2)==0) {
    t:=t+1;
    q:=iquo(q,2);
  }
  //ainsi n-1=q*2^t;
  c:=20;
  p:=true;
  while ((c>0) and p) {
    //rand(k) renvoie un nombre entier de [0,k-1] si k<999999999;
    if (n<=10^9) {
      a:=2+rand(n-2);
    } 
    else {
      a:=2+rand(999999999);
    }
    ex:=0;
    b:=powmod(a,q,n);
    //si b!=1 on regarde si b^{2^e}=-1 mod n (e=0..t-1) ;
    if (b!=1) {
      while ((b!=1) and (b!=n-1) and (ex<=t-2)) {
        b:=powmod(b,2,n);
        ex:=ex+1;
      }
      //si b!=n-1 c'est que n n'est pas premier ;
      if (b!=n-1) {
        p:=false;
      }
    }
    c:=c-1;
  }
  return(p);
};
,
// fltk N4xcas10Log_OutputE 34 485 1031 1

]
,
// fltk 7Fl_Tile 34 488 1031 74
[
// fltk N4xcas19Multiline_Input_tabE 34 488 1031 30
miller(561)
,
// fltk N4xcas10Log_OutputE 34 518 1031 1

,
// fltk N4xcas8EquationE 34 519 1031 43
false
]
,
// fltk 7Fl_Tile 34 564 1031 74
[
// fltk N4xcas19Multiline_Input_tabE 34 564 1031 30
rabin(561)
,
// fltk N4xcas10Log_OutputE 34 594 1031 1

,
// fltk N4xcas8EquationE 34 595 1031 43
false
]
,
// fltk 7Fl_Tile 34 640 1031 31
[
// fltk N4xcas19Multiline_Input_tabE 34 640 1031 30

,
// fltk N4xcas10Log_OutputE 34 670 1031 1

]
